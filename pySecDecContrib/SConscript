Import("env")

cuba = "Cuba-4.2"
form = "form-4.2.1"
gsl = "gsl-2.5"
nauty = "nauty26r7"

contrib_extra_source = File(Split(f"""
    SConscript
    {gsl}.list
"""))

contrib = []

contrib += \
    File(["__init__.py"])

contrib += \
    Install("include", "qmc.hpp")

contrib += \
    Install("include/secdecutil", Glob("util/secdecutil/*hpp")) + \
    Install("include/secdecutil/integrators", Glob("util/secdecutil/integrators/*hpp"))

contrib += \
    env.Command(["lib/libcuba.a", "include/cuba.h"], [f"{cuba}.tar.gz"], f"""
        tar -xf {cuba}.tar.gz
        # Prevent Cuba from even trying to invoke Mathematica's mcc.
        sed -i 's/mcc/false/g' {cuba}/configure
        cd {cuba} && ./configure CFLAGS="-O3 -fomit-frame-pointer -ffast-math -fno-finite-math-only -fexceptions -fcommon -fPIC"
        make -C {cuba} libcuba.a
        cp -a {cuba}/libcuba.a lib/
        cp -a {cuba}/cuba.h include/
        rm -rf {cuba}
    """, chdir="pySecDecContrib")

contrib += \
    env.Command(["bin/form", "bin/tform"], [f"{form}.tar.gz"], f"""
        tar -xf {form}.tar.gz
        cd {form} && ./configure
        make -C {form}
        cp -a {form}/sources/form {form}/sources/tform bin/
        strip bin/form bin/tform
        rm -rf {form}
    """, chdir="pySecDecContrib")

gsl_files = open(f"{gsl}.list", "r").read().splitlines()
contrib += \
    env.Command(gsl_files, [f"{gsl}.tar.gz"], f"""
        tar -xf {gsl}.tar.gz
        cd {gsl} && ./configure \
            --prefix=$$PWD/../install \
            --libdir=$$PWD/../lib \
            --includedir=$$PWD/../include \
            --bindir=$$PWD/../bin \
            --disable-shared --enable-static --with-pic
        make -C {gsl}
        make -C {gsl} install
        rm -rf {gsl}
    """, chdir="pySecDecContrib")

contrib += \
    env.Command("bin/dreadnaut", [f"{nauty}.tar.gz"], f"""
        tar -xf {nauty}.tar.gz
        cd {nauty} && ./configure
        make -C {nauty} dreadnaut
        cp -a {nauty}/dreadnaut bin/dreadnaut
        strip bin/dreadnaut
        rm -rf {nauty}
    """, chdir="pySecDecContrib")

Return("contrib")
