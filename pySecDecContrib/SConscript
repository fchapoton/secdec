Import("env")

cuba = "Cuba-4.2"
form = "form-4.2.1"
gsl = "gsl-2.5"
nauty = "nauty26r7"

contrib_extra_source = File(Split(f"""
    SConscript
    {gsl}.list
"""))

contrib = []

contrib += \
    File(["__init__.py"])

contrib += \
    Install("include", "qmc.hpp")

contrib += \
    Install("include/secdecutil", Glob("util/secdecutil/*hpp")) + \
    Install("include/secdecutil/integrators", Glob("util/secdecutil/integrators/*hpp"))

cuba_files = ["lib/libcuba.a", "include/cuba.h"]
contrib += \
    env.Command(cuba_files, [f"{cuba}.tar.gz"], f"""
        cd pySecDecContrib && \
        tar -xf {cuba}.tar.gz && \
        sed -i 's/mcc/false/g' {cuba}/configure && \
        cd {cuba} && \
        ./configure CFLAGS="-O3 -fomit-frame-pointer -ffast-math -fno-finite-math-only -fexceptions -fcommon -fPIC" && \
        make libcuba.a && \
        cp -a libcuba.a ../lib/ && \
        cp -a cuba.h ../include/ && \
        cd .. && \
        rm -rf {cuba}
    """)

form_files = ["bin/form", "bin/tform"]
contrib += \
    env.Command(form_files, [f"{form}.tar.gz"], f"""
        cd pySecDecContrib && \
        tar -xf {form}.tar.gz && \
        cd {form} && \
        ./configure \
            --prefix="$$PWD/../install" \
            --libdir="$$PWD/../lib" \
            --includedir="$$PWD/../include" \
            --bindir="$$PWD/../bin" \
            --without-gmp \
            --without-zlib && \
        make && \
        make install && \
        cd .. && \
        rm -rf {form}
    """)

gsl_files = open(f"{gsl}.list").read().splitlines()
contrib += \
    env.Command(gsl_files, [f"{gsl}.tar.gz"], f"""
        cd pySecDecContrib && \
        tar -xf {gsl}.tar.gz && \
        cd {gsl} && \
        ./configure \
            --prefix="$$PWD/../install" \
            --libdir="$$PWD/../lib" \
            --includedir="$$PWD/../include" \
            --bindir="$$PWD/../bin" \
            --disable-shared --enable-static --with-pic && \
        make && \
        make install && \
        cd .. && \
        rm -rf {gsl}
    """)

nauty_files = ["bin/dreadnaut"]
contrib += \
    env.Command(nauty_files, [f"{nauty}.tar.gz"], f"""
        cd pySecDecContrib && \
        tar -xf {nauty}.tar.gz && \
        cd {nauty} && \
        ./configure && \
        make dreadnaut && \
        strip dreadnaut && \
        cp -a dreadnaut ../bin/dreadnaut && \
        cd .. && \
        rm -rf {nauty}
    """)

Return("contrib")
